/* stlvec.pure: Pure interface to C++ STL Vectors
    
Copyright (c) 2011 by Peter Summerland <p.summerland@gmail.com>.

All rights reserved.

This software is is part of pure-stlvec, an addon to the Pure Programming
Language (http://code.google.com/p/pure-lang/).

This software is distributed under a BSD-style license in the hope that it
will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the COPYING file
included with the pure-stlvec distribution package for details.

*/
 
using stlbase;

namespace stl;

/*** C++ support *****************************************************/

private extern void* sv_make_empty();
private extern void* sv_make_from_xs(expr* xs_or_sm);
private extern void* sv_make_n(expr* xs, int n);
private extern void  sv_delete(void* vec);
private extern void* sv_dup(expr* rng);
private extern expr* sv_vector(expr* rng);
private extern void  sv_reserve(void* vec, int n);
private extern int   sv_size(void* vec);
private extern int   sv_iter_size(expr* rng);
private extern bool  sv_empty(void* vec);
private extern int   sv_capacity(void* vec);
private extern expr* sv_front(void* vec);
private extern expr* sv_back(void* vec);
private extern expr* sv_list(expr* rng);
private extern void  sv_push_back(void* vec, expr* val);
private extern void  sv_pop_back(void* vec);
private extern void  sv_splice(void* vec, int b, expr* xs_rng);
private extern void  sv_erase(expr* rng);
private extern void  sv_clear(void* vec);
private extern bool  sv_allpairs(expr* fun, expr* rng, expr* rng);
private extern expr* sv_listmap(expr* fun, expr* rng);
private extern expr* sv_listcatmap(expr* fun, expr* rng);
private extern expr* sv_foldl(expr* fun, expr* val, expr* rng);
private extern expr* sv_foldl1(expr* fun, expr* rng);
private extern expr* sv_foldr(expr* fun, expr* val, expr* rng);
private extern expr* sv_foldr1(expr* fun, expr* rng);

extern expr* sv_get(void* vec, int pos);
extern void  sv_put(void* vec, int pos, expr* val);

extern void stl_set_sv_trace(bool enable) = set_sv_trace;
extern bool stl_sv_trace_enabled() = sv_trace_enabled;

/*** Private functions on stlvecs *****************************************/

private get_sv bad_selfwrite ensure_beg ensure_end;

get_sv (STLVEC v) = v;
get_sv (CONST_STLVEC v) = v;

bad_selfwrite src::svrng trg::svrng = 
  (~typep mutable_stlvec src) && (get_sv src == get_sv trg);

ensure_beg v b = if b >= 0 && b < sv_size v then b
                 else throw out_of_bounds;

ensure_end v e = if e == svend then sz 
                 else if e < 0 || e > sz then throw out_of_bounds 
                 else e when sz = sv_size v end;


/*** Public functions on stlvec *****************************************/

::const_stlvec (STLVEC v) = (CONST_STLVEC v);
::const_stlvec (CONST_STLVEC v) = (CONST_STLVEC v);
::const_stlvec x = ::const_stlvec (::stlvec x);

::emptystlvec = STLVEC (sentry sv_delete (sv_make_empty));

::stlvec sv::svrng = STLVEC (sentry sv_delete (sv_dup sv));
::stlvec v = STLVEC(sentry sv_delete(sv_make_from_xs (smatrix v))) if vectorp v;
::stlvec xs::list = STLVEC (sentry sv_delete (sv_make_from_xs xs));

::mkstlvec x n::int = STLVEC (sentry sv_delete (sv_make_n x n));

reserve (STLVEC v) i::int = sv_reserve v i;

capacity (STLVEC v) |
capacity (CONST_STLVEC v) = sv_capacity v;

::null (STLVEC v) |
::null (CONST_STLVEC v) = sv_empty v;

(::!) (STLVEC v) p::int |
(::!) (CONST_STLVEC v) p::int = sv_get v p;

(::#) (STLVEC v) |
(::#) (CONST_STLVEC v) = sv_size v;

bounds (csv@(CONST_STLVEC v),b::int,e::int) = 
  csv, ensure_beg v b, ensure_end v e;
bounds (csv@(CONST_STLVEC v),b::int) = 
  csv, p, p when p = ensure_beg v b end;
bounds csv@(CONST_STLVEC v) = 
  csv, 0, sv_size v;
bounds (sv@(STLVEC v),b::int,e::int) =
  sv, ensure_beg v b, ensure_end v e;
bounds (sv@(STLVEC v),b::int) = 
  sv, p, p when p = ensure_beg v b end;
bounds sv@(STLVEC v) = 
  sv, 0, sv_size v;

::first (STLVEC v) |
::first (CONST_STLVEC v) = sv_front v;

::last (STLVEC v) |
::last (CONST_STLVEC v) = sv_back v;

::append sv@(STLVEC v) x = sv_push_back v x $$ sv;

::rmfirst sv@(STLVEC v) = sv_erase (STLVEC v, 0, 1) $$ sv;

::rmlast sv@(STLVEC v) = sv_pop_back v $$ sv;

::update sv@(STLVEC v) p::int x = sv_put v p x $$ sv;

::erase rng@(STLVEC v, f::int, l::int) = sv_erase rng $$ STLVEC v;

::erase (STLVEC v, p::int) = erase (STLVEC v, p, p+1);

::erase sv@(STLVEC v) = sv_clear v $$ sv;

::insert (STLVEC v, p::int) rng::svrng = sv_splice v p rng $$ STLVEC v;
::insert (STLVEC v, p::int) xs::list = sv_splice v p xs $$ STLVEC v;

allpairs bin_fun rng1::svrng rng2::svrng = sv_allpairs bin_fun rng1 rng2;

(::==) sv1::stlvec sv2::svrng = sv_allpairs (==) sv1 sv2;

(::~=) sv1::stlvec sv2::svrng = ~sv_allpairs (==) sv1 sv2;

::map f rng::svrng = sv_listmap f rng;

::listmap f rng::svrng = sv_listmap f rng;  // for list comprehensions

::catmap f rng::svrng = sv_listcatmap f rng;

::foldl f x rng::svrng = sv_foldl f x rng;

::foldl1 f rng::svrng = sv_foldl1 f rng;

::foldr f x rng::svrng = sv_foldr f x rng;

::foldr1 f rng::svrng = sv_foldr1 f rng;

::members rng::svrng = sv_list rng;

::list sv::stlvec = members sv;

::do f rng::svrng = sv_foldl (\_ x->f x) () rng $$ ();

::filter p rng::svrng = sv_foldr f [] rng with
  f x xs = case p x of
    1 = x:xs;
    0 = xs;
    y = throw failed_cond;
  end;
end;

::rowmap f rng::svrng = rowcat (sv_listmap f rng);

::rowcatmap f rng::svrng = rowcat (sv_listmap f rng);

::colmap f rng::svrng = colcat (sv_listmap f rng);

::colcatmap f rng::svrng = colcat (sv_listmap f rng);

vector rng::svrng = sv_vector rng;
