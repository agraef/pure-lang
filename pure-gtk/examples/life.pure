/* Conway's game of life in Pure. This is the same program as in the Pure
   distribution, but uses GTK+Cairo for graphical output. */

/* Set the following flag to true to wrap around at corners (torus topology).
   (Note that this eventually wreaks havoc with the glider gun example because
   the gliders destroy the gun after a while.) */

const wrap = true;

/* Two sample boards. Note that a board is denoted by a list of coordinate
   pairs of the live cells. The algorithm works directly with this
   representation which is more efficient than a matrix representation unless
   the board becomes very crowded. */

let two_gliders =
  [(6,4),(6,5),(6,6),(5,6),(4,5),
   (30,4),(31,4),(32,4),(32,5),(31,6)];

let glider_gun =
  [(11,16),(12,16),(11,17),(12,17),
   (21,16),(21,17),(21,18),
   (22,15),(22,19),
   (23,14),(23,20),(24,14),(24,20),
   (25,17),
   (26,19),(26,15),
   (27,16),(27,17),(27,18),(28,17),
   (31,16),(32,16),(31,15),(32,15),(31,14),(32,14),
   (33,17),(33,13),
   (35,17),(35,13),(35,18),(35,12),
   (45,15),(46,15),(45,14),(46,14)];

/* Define an order on coordinate pairs so that we can use them as keys in
   dictionaries and sets. */

(x1::int,y1::int)<(x2::int,y2::int) = x1<x2 || x1==x2 && y1<y2;
(x1::int,y1::int)>(x2::int,y2::int) = x1>x2 || x1==x2 && y1>y2;

using dict, set;

/* The core of the algorithm which, given an nxm board of cells, computes the
   next generation according to the following rules: (1) A live cell stays
   alive if it has 2 or 3 live cells in its neighborhood. (2) A dead cell
   becomes live if it has exactly 3 live neighbors. */

next (n::int,m::int) b = c when
  counts = list $ foldl (\d p->foldl count d (nb p)) emptydict b;
  alive = member (set b);
  c = [x,y | (x::int,y::int)=>k::int = counts; k==3 || k==2 && alive (x,y)];
end with
  // count neighbors
  count d (x::int,y::int) = update d (x,y) (getval d (x,y)+1);
  getval d x = if member d x then d!x else 0;
  // compute the neighborhood of a cell
  nb (x::int,y::int)
    = [(u+n) mod n,(v+n) mod m | u::int = x-1..x+1; v::int = y-1..y+1;
      	     	 	         (u,v) ~= (x,y)] if wrap;
    = [u,v | u::int = x-1..x+1; v::int = y-1..y+1; u<n; v<m; (u,v) ~= (x,y)];
end;

/* Graphics output. */

public animate box clr; // defined below

// Some colors to be used with the drawing operations below.
const black = (0.0,0.0,0.0);
const white = (1.0,1.0,1.0);
const red   = (1.0,0.0,0.0);
const green = (0.0,1.0,0.0);
const blue  = (0.0,0.0,1.0);

// Preferred foreground and background colors, adjust as needed.
//let fg:bg = black:white;
let fg:bg = green:black;

life (n,m) b = animate "Pure LIFE" draw (next (n,m)) 100 b with
  draw d@(cr,w::int,h::int) b = () when
    sx::double = w/n; sy::double = h/m;
    clr d; do draw_cell b with
      draw_cell (x::int,y::int) = box d fg (x*sx,y*sy) (sx,sy);
    end;
  end;
end;

/****************************************************************************/

/* Cairo interface. */

using ffi, gtk, glib, cairo;

const GTK_WINDOW = pointer_cast $ pointer_tag "GtkWindow*";
const GTK_CONTAINER = pointer_cast $ pointer_tag "GtkContainer*";
const GTK_WIDGET = pointer_cast $ pointer_tag "GtkWidget*";
const GDK_DRAWABLE = pointer_cast $ pointer_tag "GdkDrawable*";

// Initialize. This also passes arguments to GTK.
gtk::init argcp argvp when
  argcp, argvp = {argc}, ptr (byte_cstring_pointer argv) with
    ptr p::pointer = pointer_cast (pointer_type p+"*") q when
      q = malloc SIZEOF_POINTER; put_pointer q $ clear_sentry p;
    end;
  end;
end;

// Convenience macro to connect a signal to a callback.
def signal_connect instance signal c_handler data =
  glib::signal_connect_data instance signal c_handler data NULL 0;

// Convenience macros to create C callbacks via ffi.
def callback cb = fclos cb FFI_DEFAULT_ABI void_t (pointer_t, pointer_t);
def event_callback cb = fclos cb FFI_DEFAULT_ABI sint_t
			(pointer_t, pointer_t, pointer_t);
def timer_callback cb = fclos cb FFI_DEFAULT_ABI sint_t pointer_t;

/* Create a main window in which we can draw stuff. The arguments are:

   title: window title.

   draw: draw callback. This gets called as draw (cr,w,h) data, where cr is
     the drawing contex, (w,h) the current dimensions and data the user-
     supplied data to be drawn.

   next: animation callback. This gets called as next data where data is the
     current data element, and is expected to return the next data element to
     be drawn.

   delta: an integer, denotes the time interval in msecs between successive
   invocations of the next function.

   The function creates a main window in which it draws the initial data, then
   updates the data in regular intervals using the next function. The draw
   function is used to redraw the current data whenever it changes or when the
   window receives an expose event. */

animate title draw next delta data = () when

  // Create the main window and define some window properties.
  window = gtk::window_new gtk::WINDOW_TOPLEVEL;
  gtk::window_set_title (GTK_WINDOW window) title;
  gtk::window_set_default_size (GTK_WINDOW window) 400 400;

  // Create the callbacks.
  destroy_cb = callback destroy;
  delete_cb = event_callback delete_event;
  expose_cb = event_callback expose_event;
  timer_cb = timer_callback timer_event;

  // Connect the callbacks to the corresponding signals.
  signal_connect window "delete-event" delete_cb NULL;
  signal_connect window "expose-event" expose_cb NULL;
  signal_connect window "destroy" destroy_cb NULL;

  // Mark the window as paintable.
  gtk::widget_set_app_paintable window true;

  // Show it.
  gtk::widget_show window;

  // Finally install a timer callback to be called every delta msecs, which
  // will cause the window to be redrawn at 1000/delta frames per sec.
  glib::timeout_add delta timer_cb window;

  // Enter the main loop.
  gtk::main;

  // Reference these here to prevent them from being destroyed before the main
  // loop is finished.
  destroy_cb, delete_cb, expose_cb, timer_cb, data;
end with

  // Callback definitions.
  delete_event (widget, event, data) = false;
  destroy (widget, data) = gtk::main_quit;

  expose_event (widget, event, _) = false when
    win = gtk::widget_get_window (GTK_WIDGET widget);
    cr = gdk::cairo_create (GDK_DRAWABLE win);
    w,h = winsize widget;
    draw (cr,w,h) (get data);
    cairo::destroy cr;
  end;

  timer_event widget = true when
    put data (next (get data));
    w,h = winsize widget;
    gtk::widget_queue_draw_area (GTK_WIDGET widget) 0 0 w h;
  end;

  // Get the dimensions of a Gtk window
  winsize widget = w!0,h!0 when
    w = {0}; h = {0};
    gtk::window_get_size (GTK_WINDOW widget) w h;
  end;
end when
  // Store the callback data in a reference so that we can update it during
  // each timer call.
  data = ref data;
end;

// Clear the drawing area.
clr d@(cr,w,h) = box d bg (0.0,0.0) (double w,double h);

// Draw a filled rectangle with the given color and dimensions.
box (cr,_,_) (r,g,b) (x,y) (w,h) = () when
  cairo::set_source_rgb cr r g b;
  cairo::rectangle cr x y w h;
  cairo::fill cr;
end;

/****************************************************************************/

// Sample main program:
life (100,100) glider_gun;
