//-- --comment c_off --resume --verbose off

/* ut_script_stlmmap.pure -- examples for basic ops on stlmmap.

   This is a Pure script with a special format that makes it possible for the
   check_eval.pure script to evaluate single lines of Pure code and compare
   the result to an expected result.
  
     $> pure -q -w
      > run check_eval.pure 
      > check "ut_script_stlmmap.pure"
    
    check will print and evaluate single line Pure statements and check
    the result against next line beginning with "//- ", if any.

   See check_eval.pure for more details.

*/

//*** Imports *********************************************************

using system, stlmmap, stlvec;
//- ()

//*** Make stlmmap ******************************************************

let smm1 = stlmmap $ zipwith (=>) ("a".."e") (1..5); members smm1;
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5]

let sv1 = stlvec $ members smm1; members sv1;
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5]

let v1 = stl::vector (smm1,"b","d"); v1;
//- {"b"=>2,"c"=>3}

let l1 = members (smm1,stl::stlbeg,"e"); l1;
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>4]

let smm2 = emptystlmmap_with (>) (>) (==); members smm2;
//- []

insert_elms smm2 ["a"=>1,"b"=>2]; members smm2;
//- ["b"=>2,"a"=>1]

let smm3 = stlmmap (sv1,1,stl::svend); members smm3;
//- ["b"=>2,"c"=>3,"d"=>4,"e"=>5]

let smm4 = stlmmap v1; members smm4;
//- ["b"=>2,"c"=>3]

let smm5 = stlmmap l1; members smm5;
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>4]

let smm6 = mkstlmmap 0 ("a".."e"); members smm6;
//- ["a"=>0,"b"=>0,"c"=>0,"d"=>0,"e"=>0]

let smm7 = emptystlmmap; members smm7;
//- []

//*** Simple Access and Update *************************************

let smm1 = stlmmap ["a"=>1,"a"=>2,"a"=>3,"d"=>4,"e"=>5]; members smm1;
//- ["a"=>1,"a"=>2,"a"=>3,"d"=>4,"e"=>5]

smm1!stl::stlbeg;
//- [1,2,3]

smm1!stl::stlend;
//- []

smm1!"a";
//- [1,2,3]

smm1!"e";
//- [5]

update smm1 "a" 100; update smm1 "x" 200; members smm1;
//- ["a"=>1,"a"=>2,"a"=>3,"a"=>100,"d"=>4,"e"=>5,"x"=>200]

smm1!"y";
//- []

stl::set_default smm1 0;
//- 0

stl::get_default smm1;
//- 1,0

smm1!"y";
//- []

members smm1;
//- ["a"=>1,"a"=>2,"a"=>3,"a"=>100,"d"=>4,"e"=>5,"x"=>200]

member smm1 "x", member smm1 "z";
//- 1,0

//*** Bidirectional iterator functions *******************************

let smm1 = stlmmap ["a"=>1,"b"=>2,"b"=>3,"d"=>4,"e"=>5]; members smm1;
//- ["a"=>1,"b"=>2,"b"=>3,"d"=>4,"e"=>5]

let kb, ke = stl::stlbeg, stl::stlend; kb, ke;
//- stl::stlbeg,stl::stlend

smm1!kb;
//- [1]

let k1 = next_key smm1 kb; k1, smm1!k1;
//- "b",[2,3]

let k2 = next_key smm1 k1; k2, smm1!k2;
//- "d",[4]

let k1 = prev_key smm1 k2; k1, smm1!k1;
//- "b",[2,3]

let k0 = prev_key smm1 k1; k0,  smm1!k0;
//- stl::stlbeg,[1]

catch id $ prev_key smm1 kb; 
//- out_of_bounds

catch id $ prev_key smm1 k0; 
//- out_of_bounds

let k4 = prev_key smm1 ke; k4, smm1!k4;
//- "e",[5]

let k3 = prev_key smm1 k4; k3, smm1!k3;
//- "d",[4]

let k4 = next_key smm1 k3; k4, smm1!k4;
//- "e",[5]

let k5 = next_key smm1 k4; k5; 
//- stl::stlend

catch id $ next_key smm1 ke; 
//- out_of_bounds

//*** Members, keys, vals, first, last ********************************

let smm1 = stlmmap ["a"=>100,"b"=>2,"c"=>3,"d"=>4,"e"=>5,"x"=>200,"y"=>0];
//- ()

members smm1;
//- ["a"=>100,"b"=>2,"c"=>3,"d"=>4,"e"=>5,"x"=>200,"y"=>0]

members (smm1,"b","e");
//- ["b"=>2,"c"=>3,"d"=>4]

members (smm1,"b","e1");
//- ["b"=>2,"c"=>3,"d"=>4,"e"=>5]

members (smm1,"0","x");
//- ["a"=>100,"b"=>2,"c"=>3,"d"=>4,"e"=>5]

members (smm1,stl::stlbeg,"x");
//- ["a"=>100,"b"=>2,"c"=>3,"d"=>4,"e"=>5]

members (smm1,"0",stl::stlend);
//- ["a"=>100,"b"=>2,"c"=>3,"d"=>4,"e"=>5,"x"=>200,"y"=>0]

keys smm1;
//- ["a","b","c","d","e","x","y"]

keys (smm1,"b","e");
//- ["b","c","d"]

keys (smm1,"b","e1");
//- ["b","c","d","e"]

keys (smm1,"0","x");
//- ["a","b","c","d","e"]

keys (smm1,stl::stlbeg,"x");
//- ["a","b","c","d","e"]

keys (smm1,"0",stl::stlend);
//- ["a","b","c","d","e","x","y"]

vals smm1;
//- [100,2,3,4,5,200,0]

vals (smm1,"b","e");
//- [2,3,4]

vals (smm1,"b","e1");
//- [2,3,4,5]

vals (smm1,"0","x");
//- [100,2,3,4,5]

vals (smm1,stl::stlbeg,"x");
//- [100,2,3,4,5]

vals (smm1,"0",stl::stlend);
//- [100,2,3,4,5,200,0]

first smm1;
//- ["a"=>100]

first (smm1,"b","e");
//- ["b"=>2]

first (smm1,"b","e1");
//- ["b"=>2]

first (smm1,"0","x");
//- ["a"=>100]

first (smm1,stl::stlbeg,"x");
//- ["a"=>100]

first (smm1,"0",stl::stlend);
//- ["a"=>100]

last smm1;
//- ["y"=>0]

last (smm1,"b","e");
//- ["d"=>4]

last (smm1,"b","e1");
//- ["e"=>5]

last (smm1,"0","x");
//- ["e"=>5]

last (smm1,stl::stlbeg,"x");
//- ["e"=>5]

last (smm1,"0",stl::stlend);
//- ["y"=>0]

//*** insert_elm - *******************************************************

let smm1 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5]; members smm1;
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>4,"e"=>5]

let smm2 = stlmmap {"k"=>11,"l"=>12,"m"=>13}; members smm2;
//- ["k"=>11,"l"=>12,"m"=>13]

insert_elm smm1 ("a"=>10); members smm1; 
//- ["a"=>1,"a"=>10,"b"=>2,"c"=>3,"d"=>4,"e"=>5]

insert_elms smm1 ["a"=>10,"f"=>6]; members smm1; 
//- ["a"=>1,"a"=>10,"a"=>10,"b"=>2,"c"=>3,"d"=>4,"e"=>5,"f"=>6]

insert_elms smm1 {"a"=>10,"g"=>7}; members smm1;
//- ["a"=>1,"a"=>10,"a"=>10,"a"=>10,"b"=>2,"c"=>3,"d"=>4,"e"=>5,"f"=>6,"g"=>7]

erase (smm1,"a"); members smm1;
//- ["b"=>2,"c"=>3,"d"=>4,"e"=>5,"f"=>6,"g"=>7]

insert_elms smm1 (smm2,"x","z"); members smm1;
//- ["b"=>2,"c"=>3,"d"=>4,"e"=>5,"f"=>6,"g"=>7]

insert_elms smm1 (smm2,"0","2"); members smm1;
//- ["b"=>2,"c"=>3,"d"=>4,"e"=>5,"f"=>6,"g"=>7]

insert_elms smm1 (smm2,"k","k"); members smm1;
//- ["b"=>2,"c"=>3,"d"=>4,"e"=>5,"f"=>6,"g"=>7]

insert_elms smm1 (smm2,"k","l"); members smm1;
//- ["b"=>2,"c"=>3,"d"=>4,"e"=>5,"f"=>6,"g"=>7,"k"=>11]

do (\k->erase (smm1,k)) ("c".."d"); members smm1;
//- ["b"=>2,"e"=>5,"f"=>6,"g"=>7,"k"=>11]

insert_elms smm1 (smm2,"k","m"); members smm1;
//- ["b"=>2,"e"=>5,"f"=>6,"g"=>7,"k"=>11,"k"=>11,"l"=>12]

insert_elms smm1 (smm2,"l",stl::stlend); members smm1;
//- ["b"=>2,"e"=>5,"f"=>6,"g"=>7,"k"=>11,"k"=>11,"l"=>12,"l"=>12,"m"=>13]

do (\k->erase (smm1,k)) ("k".."m"); members smm1;
//- ["b"=>2,"e"=>5,"f"=>6,"g"=>7]

insert_elms smm1 ["a"=>10,"b"=>21,"b"=>20]; members smm1;
//- ["a"=>10,"b"=>2,"b"=>21,"b"=>20,"e"=>5,"f"=>6,"g"=>7]

insert_elms smm1 {"b"=>20,"b"=>22}; members smm1;
//- ["a"=>10,"b"=>2,"b"=>21,"b"=>20,"b"=>20,"b"=>22,"e"=>5,"f"=>6,"g"=>7]

catch id (insert_elm smm1 ("e")); //can only insert rocket pairs
//- bad_argument

//*** Range specifications - corner cases *******************************

let smm1 = stlmmap ["a"=>1,"a"=>1,"b"=>2,"b"=>2,"b"=>2,"e"=>3,"x"=>10,"x"=>10];
//- ()

members smm1;
//- ["a"=>1,"a"=>1,"b"=>2,"b"=>2,"b"=>2,"e"=>3,"x"=>10,"x"=>10]

members (smm1,"0");
//- []

members (smm1,stl::stlbeg);
//- ["a"=>1,"a"=>1]

members (smm1,"a");
//- ["a"=>1,"a"=>1]

members (smm1,"e");
//- ["e"=>3]

members (smm1,"f");
//- []

members (smm1,"x");
//- ["x"=>10,"x"=>10]

members (smm1,"z");
//- []

members (smm1,"0","0");
//- []

members (smm1,"a","0");
//- []

members (smm1,stl::stlbeg,"0");
//- []

members (smm1,stl::stlbeg,"c");
//- ["a"=>1,"a"=>1,"b"=>2,"b"=>2,"b"=>2]

members (smm1,stl::stlbeg,"f");
//- ["a"=>1,"a"=>1,"b"=>2,"b"=>2,"b"=>2,"e"=>3]

members (smm1,stl::stlbeg,"x");
//- ["a"=>1,"a"=>1,"b"=>2,"b"=>2,"b"=>2,"e"=>3]

members (smm1,stl::stlbeg,stl::stlend);
//- ["a"=>1,"a"=>1,"b"=>2,"b"=>2,"b"=>2,"e"=>3,"x"=>10,"x"=>10]

members (smm1,stl::stlend,stl::stlend);
//- []

members (smm1,"0",stl::stlend);
//- ["a"=>1,"a"=>1,"b"=>2,"b"=>2,"b"=>2,"e"=>3,"x"=>10,"x"=>10]

members (smm1,"c",stl::stlend);
//- ["e"=>3,"x"=>10,"x"=>10]

members (smm1,"c","z");
//- ["e"=>3,"x"=>10,"x"=>10]

members (smm1,"b","x");
//- ["b"=>2,"b"=>2,"b"=>2,"e"=>3]

members (smm1,"b","f");
//- ["b"=>2,"b"=>2,"b"=>2,"e"=>3]

members (smm1,stl::stlend,stl::stlbeg);
//- []

members (smm1,"z","c");
//- []

members (smm1,"z","x");
//- []

members (smm1,"z","z");
//- []

members (smm1,"x","c");
//- []

members (smm1,"x","0");
//- []

members (smm1,"x","f");
//- []

//*** rmfirst, rmlast, erase *******************************************

let smm1 = stlmmap ["a"=>100,"b"=>2,"c"=>3,"d"=>4,"e"=>5,"x"=>200,"y"=>0];
//- ()

members smm1;
//- ["a"=>100,"b"=>2,"c"=>3,"d"=>4,"e"=>5,"x"=>200,"y"=>0]

let smm2 = stlmmap ["a"=>1,"b"=>2,"c"=>3];
//- ()

rmfirst smm1; rmlast smm1; members smm1;
//- ["b"=>2,"c"=>3,"d"=>4,"e"=>5,"x"=>200]

rmfirst (smm1,"e","z"); members smm1;
//- ["b"=>2,"c"=>3,"d"=>4,"x"=>200]

rmlast (smm1,"a","x"); members smm1;
//- ["b"=>2,"c"=>3,"x"=>200]

erase (smm1,"c","e"); members smm1;
//- ["b"=>2,"x"=>200]

erase smm1; members smm1;
//- []

stl::null smm1;
//- 1

insert_elm smm1 ("e"=>5); members smm1;
//- ["e"=>5]

stl::null smm1;
//- 0

insert_elms smm1 smm2; members smm1;
//- ["a"=>1,"b"=>2,"c"=>3,"e"=>5]

stl::null (smm1,"b","c");
//- 0

stl::null (smm1,"b","b");
//- 1

let smm3 = stlmmap ["f"=>6,"g"=>7,"h"=>8,"i"=>9]; members smm3;
//- ["f"=>6,"g"=>7,"h"=>8,"i"=>9]

insert_elms smm1 (smm3,"g","i"); members smm1;
//- ["a"=>1,"b"=>2,"c"=>3,"e"=>5,"g"=>7,"h"=>8]

erase (smm1,"h"); members smm1;
//- ["a"=>1,"b"=>2,"c"=>3,"e"=>5,"g"=>7]

insert_elms smm1 ["e"=>7,"e"=>6]; members smm1;
//- ["a"=>1,"b"=>2,"c"=>3,"e"=>5,"e"=>7,"e"=>6,"g"=>7]

//*** delete, delete_all, delete_if, delete_all_if

let smm1 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>5,"d"=>6,"d"=>7,"d"=>8]; 
//- ()

members smm1;
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>5,"d"=>6,"d"=>7,"d"=>8]

erase_if (\(_=>v)->v==1) smm1; members smm1;
//- ["b"=>2,"c"=>3,"d"=>5,"d"=>6,"d"=>7,"d"=>8]

erase_if (\(_=>v)->v==6) (smm1,"d"); members smm1;
//- ["b"=>2,"c"=>3,"d"=>5,"d"=>7,"d"=>8]

erase_if (\(_=>v)->v<8) (smm1,"d"); members smm1;
//- ["b"=>2,"c"=>3,"d"=>8]

//*** delete, delete_all, delete_if, delete_val *************************

let sm1 = stlmmap ["a"=>1,"a"=>2,"a"=>3,"b"=>1,"b"=>2,"b"=>3,"c"=>1,"c"=>2,"c"=>3,"c"=>4];
//- ()

delete sm1 "0"; members sm1;
//- ["a"=>1,"a"=>2,"a"=>3,"b"=>1,"b"=>2,"b"=>3,"c"=>1,"c"=>2,"c"=>3,"c"=>4]

delete sm1 "a"; members sm1;
//- ["a"=>2,"a"=>3,"b"=>1,"b"=>2,"b"=>3,"c"=>1,"c"=>2,"c"=>3,"c"=>4]

delete_all sm1 "z"; members sm1;
//- ["a"=>2,"a"=>3,"b"=>1,"b"=>2,"b"=>3,"c"=>1,"c"=>2,"c"=>3,"c"=>4]

delete_all sm1 "b"; members sm1;
//- ["a"=>2,"a"=>3,"c"=>1,"c"=>2,"c"=>3,"c"=>4]

delete_val sm1 ("a"=>10); members sm1;
//- ["a"=>2,"a"=>3,"c"=>1,"c"=>2,"c"=>3,"c"=>4]

delete_val sm1 ("a"=>3); members sm1;
//- ["a"=>2,"c"=>1,"c"=>2,"c"=>3,"c"=>4]

delete_if (\(_=>v)->v > 10) sm1 "c"; members sm1;
//- ["a"=>2,"c"=>1,"c"=>2,"c"=>3,"c"=>4]

delete_if (\(_=>v)->v mod 2) sm1 "c"; members sm1;
//- ["a"=>2,"c"=>2,"c"=>3,"c"=>4]

delete_if (\(_=>v)->v mod 2) sm1 "c"; members sm1;
//- ["a"=>2,"c"=>2,"c"=>4]

//*** compare ********************************************************

let smm1 = stlmmap ["a"=>1,"b"=>2,"b"=>3,"b"=>4,"e"=>5];
//- ()

let smm2 = stlmmap ["a"=>2,"b"=>2,"b"=>3,"b"=>4,"e"=>5];
//- ()

let smm3 = stlmmap ["a"=>0,"b"=>2,"b"=>3,"b"=>4,"e"=>5];
//- ()

let smm4 = stlmmap ["a"=>1,"b"=>2,"b"=>4,"b"=>3,"e"=>5];
//- ()

let smm5 = stlmmap ["a"=>1,"b"=>2,"b"=>3,"b"=>4,"e"=>6];
//- ()

let smm6 = stlmmap ["b"=>2,"c"=>3,"d"=>4,"e"=>4];
//- ()

let smm7 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4];
//- ()

let smm8 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"e"=>5];
//- ()

let smm9 = emptystlmmap;
//- ()

smm1 < smm1;
//- 0

smm1 < smm2;
//- 1

smm1 < smm3;
//- 0

smm1 < smm4;
//- 1

smm1 < smm5;
//- 1

smm1 < smm6;
//- 1

smm1 < smm7;
//- 1

smm1 < smm8;
//- 1

smm1 < smm9;
//- 0

smm2 < smm1;
//- 0

smm3 < smm1;
//- 1

smm4 < smm1;
//- 0

smm5 < smm1;
//- 0

smm6 < smm1;
//- 0

smm7 < smm1;
//- 0

smm8 < smm1;
//- 0

smm9 < smm1;
//- 1

//*** equal ************************************************************

let smm1 = stlmmap ["a"=>1,"b"=>2,"b"=>3,"b"=>4,"e"=>5];
//- ()

let smm2 = stlmmap ["a"=>2,"b"=>2,"b"=>3,"b"=>4,"e"=>5];
//- ()

let smm3 = stlmmap ["a"=>0,"b"=>2,"b"=>3,"b"=>4,"e"=>5];
//- ()

let smm4 = stlmmap ["a"=>1,"b"=>2,"b"=>4,"b"=>3,"e"=>5];
//- ()

let smm5 = stlmmap ["a"=>1,"b"=>2,"b"=>3,"b"=>4,"e"=>6];
//- ()

let smm6 = stlmmap ["b"=>2,"c"=>3,"d"=>4,"e"=>4];
//- ()

let smm7 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4];
//- ()

let smm8 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"e"=>5];
//- ()

let smm9 = emptystlmmap;
//- ()

smm1 ==  smm1;
//- 1

smm1 ==  smm2;
//- 0

smm1 ==  smm3;
//- 0

smm1 ==  smm4;
//- 0

smm1 ==  smm5;
//- 0

smm1 ==  smm6;
//- 0

smm1 ==  smm7;
//- 0

smm1 ==  smm8;
//- 0

smm1 ==  smm9;
//- 0

smm2 ==  smm1;
//- 0

smm3 ==  smm1;
//- 0

smm4 ==  smm1;
//- 0

smm5 ==  smm1;
//- 0

smm6 ==  smm1;
//- 0

smm7 ==  smm1;
//- 0

smm8 ==  smm1;
//- 0

smm9 ==  smm1;
//- 0

smm9 ==  smm9;
//- 1

//*** operators *********************************************************

let smm1 = stlmmap ["a"=>1,"b"=>2,"b"=>3,"b"=>4,"e"=>5];
//- ()

let smm2 = stlmmap ["a"=>1,"b"=>2,"b"=>3,"b"=>3,"e"=>5];
//- ()

let smm3 = emptystlmmap;
//- ()

smm1 < smm1;
//- 0

smm1 <= smm1;
//- 1

smm1 > smm1;
//- 0

smm1 >= smm1;
//- 1

smm1 == smm1;
//- 1

smm1 ~= smm1;
//- 0

smm1 < smm2;
//- 0

smm1 <= smm2;
//- 0

smm1 > smm2;
//- 1

smm1 >= smm2;
//- 1

smm1 == smm2;
//- 0

smm1 ~= smm2;
//- 1

smm1 < smm3;
//- 0

smm1 <= smm3;
//- 0

smm1 > smm3;
//- 1

smm1 >= smm3;
//- 1

smm1 == smm3;
//- 0

smm1 ~= smm3;
//- 1

smm1 < smm1;
//- 0

smm1 <= smm1;
//- 1

smm1 > smm1;
//- 0

smm1 >= smm1;
//- 1

smm1 == smm1;
//- 1

smm1 ~= smm1;
//- 0


// ** bounds, size *******************************************************

let smm1 = stlmmap ["a"=>1,"b"=>2,"b"=>3,"b"=>1,"c"=>3,"d"=>4]; members smm1;
//- ["a"=>1,"b"=>2,"b"=>3,"b"=>1,"c"=>3,"d"=>4]

let smm2, lb, ub = stl::bounds smm1; (lb, ub);
//- stl::stlbeg,stl::stlend

let smm2, lb, ub = stl::bounds (smm1,"a"); (lb, ub);
//- stl::stlbeg,"b"

let smm2, lb, ub = stl::bounds (smm1,"a1"); (lb, ub);
//- stl::stlend,stl::stlend

let smm2, lb, ub = stl::bounds (smm1,"b"); (lb, ub);
//- "b","c"

members (smm2, lb, ub);
//- ["b"=>2,"b"=>3,"b"=>1]

let smm2, lb, ub = stl::bounds (smm1,"a","x"); (lb, ub);
//- stl::stlbeg,stl::stlend

let smm2, lb, ub = stl::bounds (smm1,"0","x"); (lb, ub);
//- stl::stlbeg,stl::stlend

let smm2, lb, ub = stl::bounds (smm1,"x","a"); (lb, ub);
//- stl::stlend,stl::stlend

let smm2, lb, ub = stl::bounds (smm1,"a","b"); (lb, ub);
//- stl::stlbeg,"b"

members (smm2, lb, ub);
//- ["a"=>1]

let smm2, lb, ub = stl::bounds (smm1,stl::stlbeg,"a"); (lb, ub);
//- stl::stlbeg,stl::stlbeg

let smm2, lb, ub = stl::bounds (smm1,stl::stlend,"x"); (lb, ub);
//- stl::stlend,stl::stlend

stl::size smm1;
//- 6

stl::size (smm1,"b","d");
//- 4

stl::size (smm1,"c",stl::stlend);
//- 2

//** map, map_keys, map_vals **************************************

let smm1 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4]; members smm1;
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>4]

map (\(k=>v)->k+1=>v+10) smm1;
//- ["b"=>11,"c"=>12,"d"=>13,"e"=>14]

map_keys (+1) smm1;
//- ["b","c","d","e"]

map_vals (+1) smm1;
//- [2,3,4,5]

map (\(k=>v)->k=>v+10) (smm1,stl::stlbeg,stl::stlend);
//- ["a"=>11,"b"=>12,"c"=>13,"d"=>14]

map (\(k=>v)->k=>v+10) (smm1,stl::stlbeg,"x");
//- ["a"=>11,"b"=>12,"c"=>13,"d"=>14]

map (\(k=>v)->k=>v+10) (smm1,stl::stlbeg,"d");
//- ["a"=>11,"b"=>12,"c"=>13]

map (\(k=>v)->k=>v+10) (smm1,stl::stlbeg,"a");
//- []

map (\(k=>v)->k=>v+10) (smm1,"0",stl::stlend);
//- ["a"=>11,"b"=>12,"c"=>13,"d"=>14]

map (\(k=>v)->k=>v+10) (smm1,"b","d");
//- ["b"=>12,"c"=>13]

map (\(k=>v)->k=>v+10) (smm1,"x","a");
//- []

//** listmap, catmap, catmap_keys, catmap_vals *************************

let smm1 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4]; members smm1;
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>4]

listmap (\(k=>v)->k+1=>v+10) smm1;
//- ["b"=>11,"c"=>12,"d"=>13,"e"=>14]

catmap (\(k=>v)->[k+1=>v+10]) smm1;
//- ["b"=>11,"c"=>12,"d"=>13,"e"=>14]

catmap (\(k=>v)->[k+1=>v+10]) (smm1,"a","b");
//- ["b"=>11]

catmap_keys (\x->[x,x+10]) smm1;
//- ["a","k","b","l","c","m","d","n"]

catmap_keys (\x->[x,x+10]) (smm1,"a","a");
//- []

catmap_vals (\x->[x,x+10]) smm1;
//- [1,11,2,12,3,13,4,14]

catmap_vals (\x->[x,x+10]) (smm1,"a","a");
//- []

[ k+1 | (k=>y) = smm1];
//- ["b","c","d","e"]

[ k=>v |  (k=>v) = smm1; v mod 2];
//- ["a"=>1,"c"=>3]

{ k+1 | (k=>y) = smm1};
//- {"b","c","d","e"}

{ {k;v} |  (k=>v) = smm1; v mod 2};
//- {"a","c";1,3}

//** foldl, foldl1, foldr, foldr1 ***********************************

let smm1 = stlmmap ["a"=>1,"a"=>2,"b"=>3,"b"=>4,"c"=>5,"d"=>6]; members smm1;
//- ["a"=>1,"a"=>2,"b"=>3,"b"=>4,"c"=>5,"d"=>6]

foldl (\{r,l} (k=>v) -> {r+k,l+v}) {"",0} smm1;
//- {"aabbcd",21}

foldl (\{r,l} (k=>v) -> {r+k,l+v}) {"",0} (smm1,"a","d");
//- {"aabbc",15}

foldl (\{r,l} (k=>v) -> {r+k,l+v}) {"",0} (smm1,"b",stl::stlend);
//- {"bbcd",18}

foldl (\{r,l} (k=>v) -> {r+k,l+v}) {"",0} (smm1,"b","d");
//- {"bbc",12}

foldl (\{r,l} (k=>v) -> {r+k,l+v}) {"",0} (smm1,"b","b");
//- {"",0}

foldl1 (\(r=>l) (k=>v) -> (r+k=>l+v)) smm1;
//- "aabbcd"=>21

foldl1 (\(r=>l) (k=>v) -> (r+k=>l+v)) (smm1,"a","d");
//- "aabbc"=>15

foldl1 (\(r=>l) (k=>v) -> (r+k=>l+v)) (smm1,"b",stl::stlend);
//- "bbcd"=>18

foldl1 (\(r=>l) (k=>v) -> (r+k=>l+v)) (smm1,"b","d");
//- "bbc"=>12

foldl1 (\(r=>l) (k=>v) -> (r+k=>l+v)) (smm1,"b","b"); (); // fails
//- ()

foldr (\(k=>v) {r,l} -> {r+k,l+v}) {"",0} smm1;
//- {"dcbbaa",21}

foldr (\(k=>v) {r,l} -> {r+k,l+v}) {"",0} (smm1,"a","d");
//- {"cbbaa",15}

foldr (\(k=>v) {r,l} -> {r+k,l+v}) {"",0} (smm1,"b",stl::stlend);
//- {"dcbb",18}

foldr (\(k=>v) {r,l} -> {r+k,l+v}) {"",0} (smm1,"b","d");
//- {"cbb",12}

foldr (\(k=>v) {r,l} -> {r+k,l+v}) {"",0} (smm1,"b","b");
//- {"",0}

foldr1 (\(r=>l) (k=>v) -> (r+k=>l+v)) smm1;
//- "aabbcd"=>21

foldr1 (\(r=>l) (k=>v) -> (r+k=>l+v)) (smm1,"a","d");
//- "aabbc"=>15

foldr1 (\(r=>l) (k=>v) -> (r+k=>l+v)) (smm1,"b",stl::stlend);
//- "bbcd"=>18

foldr1 (\(r=>l) (k=>v) -> (r+k=>l+v)) (smm1,"b","d");
//- "bbc"=>12

foldr1 (\(r=>l) (k=>v) -> (r+k=>l+v)) (smm1,"b","b"); (); // fails
//- ()

//*** do, filter *******************************************************

let smm1 = stlmmap ["a"=>1,"b"=>2,"c"=>3,"d"=>4]; members smm1;
//- ["a"=>1,"b"=>2,"c"=>3,"d"=>4]

let smm2 = emptystlmmap_with (>) (>) (==); [];
//- []

do (\(k=>v) -> update smm2 k v) smm1; members smm2;
//- ["d"=>4,"c"=>3,"b"=>2,"a"=>1]

erase smm2; do (\(k=>v) -> update smm2 k v) (smm1,"a","d"); members smm2;
//- ["c"=>3,"b"=>2,"a"=>1]

erase smm2; do (\(k=>v) -> update smm2 k v) (smm1,"b","x"); members smm2;
//- ["d"=>4,"c"=>3,"b"=>2]

erase smm2; do (\(k=>v) -> update smm2 k v) (smm1,"0","0"); members smm2;
//- []

erase smm2; do (\(k=>v) -> update smm2 k v) (smm1,"x","x"); members smm2;
//- []

erase smm2; do (\(k=>v) -> update smm2 k v) (smm1,"d","x"); members smm2;
//- ["d"=>4]

filter (\(k=>v) -> v mod 2) smm1;
//- ["a"=>1,"c"=>3]

filter (\(k=>v) -> v mod 2) (smm1,"a","c");
//- ["a"=>1]

filter (\(k=>v) -> v mod 2) (smm1,"c",stl::stlend);
//- ["c"=>3]

filter (\(k=>v) -> v mod 2) (smm1,"d","d");
//- []

//** Set operations ******************************************************

let smm1 = stlmmap ["a"=>11,"a"=>12,"b"=>13,"c"=>14,"d"=>15]; members smm1;
//- ["a"=>11,"a"=>12,"b"=>13,"c"=>14,"d"=>15]

let smm2 = stlmmap ["a"=>11,"a"=>22,"a"=>23,"b"=>22,"b"=>23]; members smm2;
//- ["a"=>11,"a"=>22,"a"=>23,"b"=>22,"b"=>23]

let smm3 = stlmmap ["a"=>11,"b"=>12,"b"=>12,"c"=>14,"c"=>14,"d"=>16];
//- ()

members smm3;
//- ["a"=>11,"b"=>12,"b"=>12,"c"=>14,"c"=>14,"d"=>16]

members (smm1 + smm1);
//- ["a"=>11,"a"=>12,"a"=>11,"a"=>12,"b"=>13,"b"=>13,"c"=>14,"c"=>14,"d"=>15,"d"=>15]

members (smm1 + smm2);
//- ["a"=>11,"a"=>12,"a"=>11,"a"=>22,"a"=>23,"b"=>13,"b"=>22,"b"=>23,"c"=>14,"d"=>15]

members (smm2 + smm1);
//- ["a"=>11,"a"=>22,"a"=>23,"a"=>11,"a"=>12,"b"=>22,"b"=>23,"b"=>13,"c"=>14,"d"=>15]

members (smm1 - smm1);
//- []

members (smm1 - smm2);
//- ["a"=>12,"b"=>13,"c"=>14,"d"=>15]

members (smm2 - smm1);
//- ["a"=>22,"a"=>23,"b"=>22,"b"=>23]

members (smm1 * smm1);
//- ["a"=>11,"a"=>12,"b"=>13,"c"=>14,"d"=>15]

members (smm1 * smm2);
//- ["a"=>11]

members (smm2 * smm1);
//- ["a"=>11]

members (smm1 / smm1);
//- []

members (smm1 / smm2);
//- ["a"=>12,"a"=>22,"a"=>23,"b"=>13,"b"=>22,"b"=>23,"c"=>14,"d"=>15]

members (smm2 / smm1);
//- ["a"=>22,"a"=>23,"a"=>12,"b"=>22,"b"=>23,"b"=>13,"c"=>14,"d"=>15]

members (stl::set_union smm1 smm1);
//- ["a"=>11,"a"=>12,"b"=>13,"c"=>14,"d"=>15]

members (stl::set_union smm1 smm2);
//- ["a"=>11,"a"=>12,"a"=>22,"a"=>23,"b"=>13,"b"=>22,"b"=>23,"c"=>14,"d"=>15]

members (stl::set_union smm2 smm1);
//- ["a"=>11,"a"=>12,"a"=>22,"a"=>23,"b"=>13,"b"=>22,"b"=>23,"c"=>14,"d"=>15]

members (stl::set_intersection smm1 smm1);
//- ["a"=>11,"a"=>12,"b"=>13,"c"=>14,"d"=>15]

members (stl::set_intersection smm1 smm2);
//- ["a"=>11]

members (stl::set_intersection smm2 smm1);
//- ["a"=>11]

members (stl::set_difference smm1 smm1);
//- []

members (stl::set_difference smm1 smm2);
//- ["a"=>12,"b"=>13,"c"=>14,"d"=>15]

members (stl::set_difference smm2 smm1);
//- ["a"=>22,"a"=>23,"b"=>22,"b"=>23]

members (stl::set_symmetric_difference smm1 smm1);
//- []

members (stl::set_symmetric_difference smm1 smm2);
//- ["a"=>12,"a"=>22,"a"=>23,"b"=>13,"b"=>22,"b"=>23,"c"=>14,"d"=>15]

//** Cache sensitive sequences ****************************************

let smm1 = stlmmap ["a"=>1,"a"=>2,"a"=>3,"d"=>4,"e"=>5,"f"=>6,"g"=>7,"h"=>8,"i"=>9,"j"=>10]
//- ()

stl::set_smm_trace 1;
//- ()

member smm1 "a";
//- 1

smm1!"a";
//- [1,2,3]

erase_if (==("a"=>1)) (smm1,"a"); members smm1;
//- ["a"=>2,"a"=>3,"d"=>4,"e"=>5,"f"=>6,"g"=>7,"h"=>8,"i"=>9,"j"=>10]

smm1!"a";
//- [2,3]

member smm1 "a";
//- 1

erase (smm1,"a"); members smm1;
//- ["d"=>4,"e"=>5,"f"=>6,"g"=>7,"h"=>8,"i"=>9,"j"=>10]

id (smm1!"a");
//- []

member smm1 "a";
//- 0

smm1!"e";
//- [5]

rmfirst smm1;
//- 1

smm1!"e";
//- [5]

member smm1 "d";
//- 0

smm1!"e"
//- [5]

rmlast smm1; members smm1;
//- ["e"=>5,"f"=>6,"g"=>7,"h"=>8,"i"=>9]

smm1!"e";
//- [5]

member smm1 "k";
//- 0

smm1!"e";
//- [5]

erase (smm1, "h", stl::stlend); members smm1;
//- ["e"=>5,"f"=>6,"g"=>7]

smm1!"e";
//- [5]

member smm1 "i";
//- 0

member smm1 "e";
//- 1

stl::set_smm_trace 0;
//- ()

//*** update_vals *******************************************************

let smm1 = stlmmap ["a"=>11,"a"=>12,"b"=>13,"c"=>14,"d"=>15]; members smm1;
//- ["a"=>11,"a"=>12,"b"=>13,"c"=>14,"d"=>15]

update_vals smm1 "a" $ map (+100) (smm1!"a"); members smm1;
//- ["a"=>111,"a"=>112,"b"=>13,"c"=>14,"d"=>15]

update_vals smm1 "a" []; members smm1;
//- ["b"=>13,"c"=>14,"d"=>15]

update_vals smm1 "e" []; members smm1;
//- ["b"=>13,"c"=>14,"d"=>15]

update_vals smm1 "e" [16,17,18,19]; members smm1;
//- ["b"=>13,"c"=>14,"d"=>15,"e"=>16,"e"=>17,"e"=>18,"e"=>19]

update_vals smm1 "e" [26,27]; members smm1;
//- ["b"=>13,"c"=>14,"d"=>15,"e"=>26,"e"=>27]

update_vals smm1 "b" [23,24,25]; members smm1;
//- ["b"=>23,"b"=>24,"b"=>25,"c"=>14,"d"=>15,"e"=>26,"e"=>27]


