/* anagram_groups.pure - print groups of words that have the same letters.

   This is a Pure version of the C++ program presented in Chapter 13, STL
   Tutorial and Reference Guide, David Musser, et., al. */

using system, stlvec, stlvec::algorithms;

print_anagrams fp = () when
  do add_pair (split "\n" (fget fp));
  stl::stable_sort word_pairs first_less;
  put_groups 0;
  stl::sort anagrams (<);
  do puts anagrams;
end with
  add_pair s = append word_pairs (key,s) when
    key = strcat $ sort (<) (list s);
  end;
  put_groups j = () if j == stl::svend;
  put_groups j = put_groups k when
    j = stl::adjacent_find (word_pairs, j, stl::svend) first_equal;
    k = if j == stl::svend then j else put_group j;
  end;
  put_group j = k when
    k = stl::find_if (word_pairs, j+1, stl::svend)
                     (first_not_equal (word_pairs!j));
     k = if k >= #word_pairs then stl::svend else k;
    append anagrams (join " " $ (map (\(_,w)->w) (word_pairs, j, k)));
  end;
  first_less (k1,_) (k2,_) = k1 < k2;
  first_equal (k1,_) (k2,_) = k1 == k2;
  first_not_equal (k1,_) (k2,_) = k1 ~= k2;
end when
  word_pairs = emptystlvec; // (word's letters sorted, word)
  anagrams = emptystlvec;   // list of words with same letters
end;

anagrams = () when
  fp = case fopen "anagram_groups_dict.txt" "r" of 
    fopen _ _ = pointer 0;
    fp = fp;
  end;
  if null fp then 
    puts "Can't open word file"
  else if feof fp then
    puts "Empty word file"
  else
    print_anagrams fp;
  fclose fp;
end;

