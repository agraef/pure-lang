#! /bin/awk -f

# This program needs GNU awk.

# Synopsis: fixdoc latex-input-file > latex-output-file

# 'fixdoc' is a little utility script which attempts to clean up some of the
# mess that rst2latex produces. Specifically, sequences of link targets seem
# to produce nested \hyperref calls for some mysterious reason, and itemize
# and description environments get split at link targets, which is rather
# inconvenient since pure-doc can't handle inlined link targets right now.

# As an added bonus, 'fixdoc' also adds LaTeX labels to link targets and
# augments index entries with page numbers. Note that the latter only works if
# the index is in its own section named 'Index'.

BEGIN { RS = ""; FS = ""; idx = 0 }
{
  # Remove nested link targets (bug in rst2latex?).
  while (gsub(/\{\\hypertarget\{[^}]*\}\{[^}]*\}\}/, "{}") > 0) { }
  x = $0
  # Merge adjacent itemize and description environments.
  x = gensub(/\\end{itemize}\n*((\\hypertarget\{[^}]*\}\{[^}]*\})*)\n*\\begin{itemize}/, "\\1", "g", x)
  x = gensub(/\\end{description}\n*((\\hypertarget\{[^}]*\}\{[^}]*\})*)\n*\\begin{description}/, "\\1", "g", x)
  # Add LaTeX labels to link targets.
  x = gensub(/\\hypertarget\{([^}]*)\}\{([^}]*)\}/, "\\0\\\\label{\\1}", "g", x)
  # Add pagerefs to entries in the Index section.
  if (match($0, /\\section\{Index\}/) > 0) {
      idx = 1
  } else if (match($0, /\\section/) > 0) {
      idx = 0
  }
  if (idx != 0) {
      x = gensub(/\\href\{\\\#([^}]*)\}\{(([^}]|\{[^}]*\})*)\}/, "\\0\\\\ \\\\ \\\\pageref*{\\1}", "g", x)
  }
  print x, "\n"
}
