
/* This is a straight port of the g2_test.c program included in the g2
   distribution. */

using g2, penguin, system;

// Convenience function to create an X11 window with a custom title.
g2_open_x11 name::string w::int h::int
  = g2_open_X11X w h (-1) (-1) name name NULL 0 0;

// Attach a physical device to some other virtual device, give feedback.
add msg vd d::int = d when
  g2_attach vd d; printf "..%s(%s)" (msg,str d);
end;
add msg vd _ = 0;

test = () when
  printf "g2 version %s\nadding" G2_VERSION;
  d = g2_open_vd; // open virtual device
  // we simply list all devices here, though some may not actually be available
  dev = {add "ps"        d (g2_open_PS   "g2_test.ps"   g2_A4 g2_PS_land),
      	 add "epsf"      d (g2_open_EPSF "g2_test.eps"),
      	 add "epsf_clip" d (g2_open_EPSF_CLIP "g2_test_clip.eps" 200 200),
      	 add "fig"       d (g2_open_FIG  "g2_test.fig"),
      	 add "gd[png]"   d (g2_open_gd   "g2_test.png"  775 575 g2_gd_png),
      	 add "gd[jpeg]"  d (g2_open_gd   "g2_test.jpg"  775 575 g2_gd_jpeg),
      	 add "x11"       d (g2_open_x11  "g2_test.pure" 775 575),
	 add "win32"     d (g2_open_win32 775 575 "g2_test" 0),
	 add "wmf32"     d (g2_open_win32 775 575 "g2_test.emf" 1)};
  puts ".";
  g2_set_auto_flush d 0;

  //g2_set_coordinate_system d 775.0 575.0 (-0.75) (-1.0);

  do test1 (0..26) with
    test1 i = () when
      g2_pen d i;
      g2_filled_circle d (i*20+10.0) 10.0 10.0; 
      g2_pen d 1;
      g2_circle d (i*20+10.0) 10.0 10.0;
      g2_string d (i*20+7.0) 21.0 (str i);
    end;
  end;

  void [test2 (dev!d) i | d = 0..#dev-1; dev!d>0; i = 0..64] with
    test2 d i = () when
      g2_move d (2*i+575.0) 5.0;
      g2_pen d (g2_ink d (i/64) 0.0 0.0);
      g2_line_r d 0.0 20.0;
      g2_pen d (g2_ink d 0.0 (i/64) 0.0);
      g2_line_r d 10.0 20.0;
      g2_pen d (g2_ink d 0.0 0.0 (i/64));
      g2_line_r d (-10.0) 20.0;
    end;
  end;

  g2_pen d 1;
  g2_line d 200.0 50.0 350.0 50.0;
  g2_line d 200.0 48.0 350.0 48.0;
  g2_line d 200.0 46.0 350.0 46.0;
  g2_line d 200.0 46.0 200.0 75.0;
  g2_line d 198.0 46.0 198.0 75.0;
  g2_line d 196.0 46.0 196.0 75.0;
  g2_string d 200.0 50.0 "012abcABC#())(\\-+~*!$%&";

  g2_pen d 1;
  do test3 (1..24) with
    test3 i = () when
      g2_line d 15.0 (i*20+50.0) 15.0 (i*20+50.0+i);
      g2_set_font_size d 12.0;
      g2_string d 20.0 (i*20+50.0) (sprintf "%2d:" i);
      g2_set_font_size d (double i);
      g2_string d 40.0 (i*20+50.0) "hello, world";
    end;
  end;

  g2_plot d 150.0 70.0;
  g2_line d 147.0 68.0 153.0 68.0;

  y = 100.0;
  g2_line d 120.0 y 170.0 (y+50);
  g2_triangle d 150.0 y 250.0 y 200.0 (y+50);
  g2_rectangle d 300.0 y 400.0 (y+50);
  g2_circle d 450.0 (y+25) 25.0;
  g2_ellipse d 550.0 (y+25) 45.0 25.0;
  g2_arc d 650.0 (y+25) 25.0 45.0 90.0 360.0;

  g2_set_dash d 2 {4.0,4.0};
  g2_line d (120.0+5) y (170.0+5) (y+50);
  g2_triangle d (150.0+10) (y+4) (250.0-10) (y+4) 200.0 (y+50-5);
  g2_rectangle d 305.0 (y+5) 395.0 (y+50-5);
  g2_circle d 450.0 (y+25) 20.0;
  g2_ellipse d 550.0 (y+25) 40.0 20.0;
  g2_arc d 650.0 (y+25) 20.0 40.0 90.0 360.0;
  g2_set_dash d 0 NULL;

  y = 200.0;
  g2_filled_triangle d 150.0 y 250.0 y 200.0 (y+50);
  g2_filled_rectangle d 300.0 y 400.0 (y+50);
  g2_filled_circle d 450.0 (y+25) 25.0;
  g2_filled_ellipse d 550.0 (y+25) 45.0 25.0;
  g2_filled_arc d 650.0 (y+25) 25.0 45.0 90.0 360.0;

  y = 300.0;
  pts = {150.0, y, 175.0, y+100, 200.0, y, 225.0, y+100, 250.0, y};
  g2_poly_line d 5 pts;
  g2_pen d 19;
  g2_b_spline d 5 pts 20;
  g2_pen d 1;

  pts = {300.0, y, 350.0, y, 375.0, y+50, 325.0, y+90, 275.0, y+50};
  g2_polygon d 5 pts;

  pts = {450.0, y, 500.0, y, 525.0, y+50, 475.0, y+90, 425.0, y+50};
  g2_filled_polygon d 5 pts;

  g2_image d 55.0  50.0 53 62 penguin;
  g2_image d 75.0 130.0 53 62 penguin;
  g2_pen d 1;

  g2_line d 225.0 448.0 (200.0+19*25) 448.0;
  do test4 (1..19) with  
    test4 i = () when
      g2_pen d (i+1);
      g2_set_line_width d (double i);
      g2_move d (200.0+i*25) 450.0;
      g2_line_to d (200.0+i*25) 550.0;
    end;
  end;
  g2_pen d 1;

  g2_set_line_width d 5.0;
  do test5 (1..9) with  
    test5 i = () when
      pts = dmatrix {k*i | k=1..3};
      g2_set_dash d 3 pts;
      g2_line d 550.0 (300.0+i*8) 750.0 (350.0+i*8); 
    end;
  end;

  g2_set_dash d 0 NULL;
  g2_set_line_width d 4.0;
  g2_arc d 740.0 180.0 25.0 100.0 (-45.0+15) (-45.0-15);
  g2_arc d 740.0 180.0 20.0  95.0 (-45.0-15) (-45.0+15);
  g2_filled_arc d 740.0 180.0 12.0 50.0 (-45.0+15) (-45.0-15);

  g2_set_line_width d 1.0;
  g2_circle d 400.0 400.0 20.0;
  g2_ellipse d 400.0 400.0 25.0 25.0;
  g2_arc d 400.0 400.0 30.0 30.0 0.0 360.0;

  g2_flush d;
  puts "Done. Press [Enter]."; gets;
  g2_close d;
end;

test;
