// Quoted specials and special patterns (Pure 0.47+).

'(\x->x+1);

foo = '(\x->x+1);
foo;

let x = '(case 99 of x::int = x+1 if x>0 end);
x; eval x;
let x = '(x+1 when x::int = 99 end);
x; eval x;
let x = '(f 99 with f x::int = x+1 end);
x; eval x;

// Manipulate a special with a function.

// swap arguments
bar (\x y -> z) = ('(\y x -> z));
let x = bar ('(\a b -> a-b));
x; eval x 2 3;

// name an argument
baz x = '(\x->x+1);
let x = baz y;
x; eval x 99;

/* Manipulate a special with a macro. These are the same as the bar and baz
   functions above, but implemented as macros. Note that the quoting of
   specials is implicit in macro arguments. */

def bar (\x y -> z) = __eval__ ('(\y x -> z));
def baz x = __eval__ ('(\x->x+1));

f = bar (\a b -> a-b); // same as f = \b a -> a-b;
f 2 3;

g = baz y; // same as g = \y -> y+1;

// Direct invocation of special macros.

f __with__ [f x --> y __when__ [y --> x+1]];
f __with__ [f x --> y __with__ [y --> x+1]];

// get_fundef and get_macdef

get_macdef (*); get_macdef ($);
get_fundef bla; get_fundef ($);

fact n = n*fact (n-1) if n>0;
       = 1 otherwise;

fact __with__ get_fundef fact;
let x = eval (fact __with__ get_fundef fact);
map x (0..10);

foldr __with__ get_fundef foldr;
let x = eval (foldr __with__ get_fundef foldr);
x (*) 1 (1..10);

// add_fundef and add_macdef

add_fundef $
'[(fold f a [] --> a),
  (fold f a (xs __as__ (_:_)) --> tick [] xs __with__
   [(tick zs (x:xs) --> tack (x:zs) (fold f a xs&) __if__ thunkp xs),
    (tick zs (x:xs) --> tick (x:zs) xs),
    (tick zs [] --> tack zs a),
    (tick zs xs --> tack zs (fold f a xs)),
    (tack (x:xs) y --> tack xs (f x y)),
    (tack [] y --> y)])];
fold (*) 1 (1..10);

add_macdef $ '[app f x --> f x];
h x y = app (+) x y;
h 2 3;

// get_typedef and add_typedef

get_typedef list;
add_typedef $ '[mylist [],mylist (_:_)];
map (typep mylist) [99,[],1..3,1:3];
