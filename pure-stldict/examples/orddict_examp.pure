
/* Some basic examples for the orddict module. This is intended to be fed to
   the interpreter in a piecemeal fashion (best done using "Send Current Line"
   in Emacs). To see the results, you can also run the script from the command
   line as follows: pure < orddict_examp.pure

   NOTE: In this directory you'll also find a log file generated by running
   this script through Pure. You can run the script yourself and compare the
   results with the log to see whether the orddict module works ok on your
   system. The Makefile has a target which automatizes this; simply run 'make
   check' in the main source directory. */

using orddict;

public bar; // free symbol used below

/* Create an orddict and populate it with some Pure data. Both keys and values
   may be arbitrary Pure expressions. The key values in a dict must all be
   ordered using the standard '<' predicate. This is also the order that will
   be used when printing an orddict or extracting its members. In the
   following examples, the predefined orders on strings and numbers will be
   used, but of course you can also use your own custom data types if you
   define '<' on them. */

let m = orddict ["foo"=>99, "bar"=>bar 4711L, "baz"=>1..5]; m;

/* Note that orddicts are printed using the format 'orddict [key=>val,...]' by
   default, which reconstructs the value when used as a literal in Pure code.
   It is also possible to define a custom pretty-printing, using your own
   preferred symbol in place of 'orddict'. In particular, the symbol may also
   be an outfix symbol so that orddicts can be printed and entered in a
   bracketed format analogous to list and matrix notation. The following code
   shows how to make this work. To these ends, we introduce a nonfix symbol
   {$$} denoting the empty orddict (note that this must be written as is, with
   no spaces between the "brackets"), and a corresponding pair of outfix
   symbols {$ $} which will be used to denote non-empty dicts. */

nonfix {$$};  // empty orddict
outfix {$ $}; // orddict brackets

// Define the above as macros which expand to proper orddict values.
// NOTE: This uses the new built-in __list__ macro from Pure 0.49 or later.

def {$$} = orddict [];
def {$ xs@(_,_) $} = orddict (__list__ xs);
def {$ x $} = orddict [x];

/* Now we can use the notation {$ key=>val, ... $} to enter orddict literals
   in Pure code, and a call to the orddict_symbol function also makes the
   orddict pretty-printer use this notation. */

orddict_symbol ({$ $});

let m = {$ "foo"=>99, "bar"=>bar 4711L, "baz"=>1..5 $}; m;

/* The pretty-printing can be changed at any time. E.g., to revert to the
   default: */

orddict_symbol orddict;
m;

// Query the dict.
m!"bar";  // => bar 4711L
keys m;   // => ["bar","baz","foo"]
vals m;   // => [bar 4711L,[1,2,3,4,5],99]
list m;   // => ["bar"=>bar 4711L,"baz"=>[1,2,3,4,5],"foo"=>99]

// Membership test.
member m "foo", member m "bar";

// Delete an element. Note that orddicts are mutable, so you need to take a
// copy if you want to preserve the original value.
let m1 = copy m;
delete m "foo"; m1;
m!"foo";  // => out_of_bounds exception
keys m; // => ["bar","baz"]
vals m; // => [bar 4711L,[1,2,3,4,5]]
list m; // => ["bar"=>bar 4711L,"baz"=>[1,2,3,4,5]]

member m "foo", member m "bar";

// Orddicts can also be used in list and matrix comprehensions.
[x,y | x=>y = m1];
{x,y | x=>y = m1; listp y};

/* You can check two orddicts for syntactic equality, which holds if and only
   if they contain exactly the same elements, in the same order. */

m===m, m===m1, m === copy m,
orddict [97=>98] === orddict [97.0=>98]; // => 1,0,1,0

/* Note that the above checks both keys and values for syntactic equality,
   which is why orddict [97=>98] and orddict [97.0=>98] are considered unequal,
   even though 97 == 97.0 in Pure and thus the two keys will be considered
   equal by orddict. */

member (orddict [97=>98]) 97.0; // => 1

/* If this bothers you, you can also check for semantic equality, which is
   somewhat less efficient, but takes into account the key order and properly
   deals with the multidict case (see below). */

m==m, m==m1, m == copy m,
orddict [97=>98] == orddict [97.0=>98]; // => 1,0,1,1

/* Note that the orddict module provides no subdict comparisons. If you need
   them, here's one way to define them in Pure instead. An orddict x is
   considered a subdict of an orddict y iff all key=>val pairs of x are also
   contained in y. */

x::orddict <= y::orddict = x===y ||
  (all (member y) xks && vals x === [y!k | k = xks] when xks = keys x end);
x::orddict >= y::orddict = x===y ||
  (all (member x) yks && vals y === [x!k | k = yks] when yks = keys y end);
x::orddict <  y::orddict = x<=y && #x<#y;
x::orddict >  y::orddict = x>=y && #x>#y;

m<=m1, m<m1, m>=m, m>m; // => 1,1,1,0

/* Orddicts can also serve as an ordered set data structure which contains only
   keys but no values. In this case the value associated with a key defaults
   to the key itself. */

let m = orddict [99, 4711L, 1.0]; m;

member m 99;
m!99;
delete m 99;
member m 99;
m!99;
insert m 99;

// You can also mix singleton elements with ordinary key-value associations.
delete m 4711L;
insert m (88=>bar 4711L);
m!99;
m!88;

/* Basic set-like operations like union, intersection etc. aren't provided by
   the module either, but you can easily implement them yourself as follows.
   Note that we take a copy of the first operand here in order to preserve
   value semantics. We also define an 'ordxdict' union type here, so that the
   definitions below work with both 'orddict' and 'ordmdict' (see below for
   the latter). */

type ordxdict x::orddict | ordxdict x::ordmdict;

x::ordxdict + y::ordxdict = foldl insert (copy x) (list y);
x::ordxdict - y::ordxdict = foldl delete (copy x) (list y);
x::ordxdict * y::ordxdict = x-(x-y);

let m1 = orddict [5,1,3,11,3];
let m2 = orddict (3..6);

m1;m2;
m1+m2; // => orddict [1,3,4,5,6,11]
m1-m2; // => orddict [1,11]
m1*m2; // => orddict [3,5]

// These operations also work with general orddicts, of course.
orddict ["foo"=>99,"baz"=>1..5] + orddict ["bar"=>bar 4711L];

/* Multidicts are supported, too. These differ from the ordinary orddicts in
   that they may contain multiple key-value associations for each key. They
   are implemented by the ordmdict type, which provides pretty much the same
   API as the orddict type. */

let m1 = ordmdict [5,1,3,11,3];
let m2 = ordmdict (3..6);

m1;m2;
m1+m2; // => ordmdict [1,3,3,3,4,5,5,6,11]
m1-m2; // => ordmdict [1,3,11]
m1*m2; // => ordmdict [3,5]

let m = ordmdict ["foo"=>99,"baz"=>1..5] + ordmdict ["baz"=>bar 4711L]; m;

/* Note that indexing works a bit differently from ordinary orddicts in that
   the list of *all* associations for a given key is returned (which may be
   empty if the key is not in the dict). */

m!!["foo","bar","baz"];

/* Like orddicts, ordmdicts can be compared for syntactic and semantic
   equality. The latter also properly deals with the case that equal keys may
   be listed in any order. (This may become slow in degenerate cases where the
   ordmdict contains much more members than distinct keys.) */

ordmdict [97=>98,97.0=>99] === ordmdict [97.0=>98,97=>99]; // => 0
ordmdict [97=>98,97.0=>99] == ordmdict [97.0=>98,97=>99]; // => 1
